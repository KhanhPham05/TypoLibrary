plugins {
    id 'java'
    id 'net.neoforged.gradle.userdev' version '[7.0, 8.0)'
}

project.version = modVersion
project.group = group

base.archivesName = modId

//minecraft.mappings channel: 'parchment', version: '2023.10.08-1.20.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
runs {
    // applies to all the run configs below
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', project.modId
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.modId
        programArgument '--nogui'
    }

    // This run config launches GameTestServer and runs all registered gametests, then exits.
    // By default, the server will crash when no gametests are provided.
    // The gametest system is also enabled by default for other run configs under the /test command.
    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.modId
    }

    data {
        // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
        // workingDirectory project.file('run-data')

        // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
        programArguments.addAll '--mod', project.modId, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}


sourceSets.main.resources.srcDir("src/generated/resources")

dependencies {
    implementation "net.neoforged:neoforged:${neoVersion}"
}

tasks.withType(ProcessResources).configureEach {
    var license = hasProperty("customLicense") ? property("customLicense") : "All Right Reversed"
    var replaceProperties = [
            minecraft_version      : minecraftVersion,
            minecraft_version_range: "[${minecraftVersion}]",
            neo_version            : neoVersion,
            neo_version_range      : neoVersionRange,
            loader_version_range   : "[47]",
            mod_id                 : modId,
            mod_name               : modName,
            mod_license            : license,
            mod_version            : modVersion,
            mod_authors            : author,
            mod_description        : modDescription,
            pack_format_number     : packFormat,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

